#Использовать v8runner
#Использовать 1commands
#Использовать logos
#Использовать tempfiles

//BSLLS-OFF: MissingVariableDescription
Перем Лог;
Перем ПутьКФайлуРаспаковки;
Перем ПутьКФайлуУпаковки;

Функция Распаковать(Знач Файл)
	
	Лог.Информация("Распаковка средствами утилиты %1", Файл);
	
	Каталог = ВременныеФайлы.СоздатьКаталог();

	Команда = Новый Команда;
	Команда.УстановитьКоманду(ПутьКФайлуРаспаковки);
	Команда.ДобавитьПараметр("-parse");
	Команда.ДобавитьПараметр(Файл);
	Команда.ДобавитьПараметр(Каталог);

	КодВозврата = Команда.Исполнить();
	
	Если КодВозврата <> 0 Тогда
		ВызватьИсключение Команда.ПолучитьВывод();
	КонецЕсли;
	
	Лог.Информация("Успешно %1 -> %2", Файл, Каталог);

	Возврат Каталог;

КонецФункции

Функция Упаковать(Знач Каталог)

	Лог.Информация("Упаковка средствами утилиты %1", Каталог);

	Файл = ВременныеФайлы.НовоеИмяФайла("cf");
	
	Команда = Новый Команда;
	Команда.УстановитьКоманду(ПутьКФайлуУпаковки);
	Команда.ДобавитьПараметр("-build");
	Команда.ДобавитьПараметр(Каталог);
	Команда.ДобавитьПараметр(Файл);

	КодВозврата = Команда.Исполнить();
	
	Если КодВозврата <> 0 Тогда
		ВызватьИсключение Команда.ПолучитьВывод();
	КонецЕсли;

	Лог.Информация("Успешно %1 -> %2", Каталог, Файл);

	Возврат Файл;

КонецФункции

Функция ВыгрузитьФайлыКонфигурации(Знач ПутьКФайлу)

	Лог.Информация("Распаковка средствами платформы %1", ПутьКФайлу);

	ВременныйКаталог = ВременныеФайлы.СоздатьКаталог();

	Конфигуратор = Новый УправлениеКонфигуратором();
	Конфигуратор.ЗагрузитьКонфигурациюИзФайла(ПутьКФайлу);
	Конфигуратор.ВыгрузитьКонфигурациюВФайлы(ВременныйКаталог);

	ВыводКоманды = Конфигуратор.ВыводКоманды();
	Если ЗначениеЗаполнено(ВыводКоманды) Тогда
		Лог.Информация(Конфигуратор.ВыводКоманды());
	КонецЕсли;

	// удаление временной базы
	УдалитьФайлы(Конфигуратор.ПутьКВременнойБазе());
	
	Лог.Информация("Успешно %1 -> %2", ПутьКФайлу, ВременныйКаталог);

	Возврат ВременныйКаталог;

КонецФункции

Процедура СравнитьКаталоги(Знач Каталог1, Знач Каталог2)

	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("ИмяФайла");
	ТаблицаЗначений.Колонки.Добавить("Наличие");

	ЗагрузитьФайлыВТаблицуНаличия(ТаблицаЗначений, Каталог1, 1);
	ЗагрузитьФайлыВТаблицуНаличия(ТаблицаЗначений, Каталог2, -1);

	ЕстьРасхождения = Ложь;

	ТаблицаЗначений.Свернуть("ИмяФайла", "Наличие");
	Для Каждого СтрокаСравнения Из ТаблицаЗначений Цикл

		Если СтрокаСравнения.Наличие > 0 Тогда
			Лог.Предупреждение("Файл %1 есть только в первом каталоге", СтрокаСравнения.ИмяФайла);
		ИначеЕсли СтрокаСравнения.Наличие < 0 Тогда
			Лог.Предупреждение("Файл %1 есть только во втором каталоге", СтрокаСравнения.ИмяФайла);
		Иначе
			Продолжить;
		КонецЕсли;

		ЕстьРасхождения = Истина;

	КонецЦикла;

	Если ЕстьРасхождения Тогда
		ВызватьИсключение НСтр("ru='Есть расхождения выгрузок конфигурации.';");
	КонецЕсли;

КонецПроцедуры

Процедура ЗагрузитьФайлыВТаблицуНаличия(Знач ТаблицаЗначений, Знач Каталог, Знач Наличие)

	Каталог = Новый Файл(Каталог);
	ФайлыКаталога = НайтиФайлы(Каталог.ПолноеИмя, "*.*");
	Для Каждого Файл Из ФайлыКаталога Цикл
		НоваяСтрока = ТаблицаЗначений.Добавить();
		НоваяСтрока.ИмяФайла = СтрЗаменить(Файл.ПолноеИмя, Каталог.ПолноеИмя, "");
		НоваяСтрока.Наличие = Наличие;
	КонецЦикла;

КонецПроцедуры

Процедура ПротестироватьКонфигурацию(Знач ПутьКФайлу)

	ФайлыДоТеста = ВременныеФайлы.Файлы();

	КаталогВыгрузкиУтилиты = Распаковать(ПутьКФайлу);
	ВыгрузкаИсходнойКонфигурации = ВыгрузитьФайлыКонфигурации(ПутьКФайлу);
	СобранныйФайл = Упаковать(КаталогВыгрузкиУтилиты);
	ВыгрузкаСобраннойКонфигурации = ВыгрузитьФайлыКонфигурации(СобранныйФайл);

	СравнитьКаталоги(ВыгрузкаИсходнойКонфигурации, ВыгрузкаСобраннойКонфигурации);

	ВременныеФайлы.УдалитьНакопленныеВременныеФайлы(ФайлыДоТеста);

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("E8Tools.V8Unpack.TestScript");
ПутьКФайлуРаспаковки = "..\V8Unpack\bin\Release\net8.0\V8Unpack.exe";
ПутьКФайлуУпаковки = ".\v8unpack.exe";
ВременныеФайлы.БазовыйКаталог = ОбъединитьПути(ТекущийКаталог(), "temp");
СоздатьКаталог(ВременныеФайлы.БазовыйКаталог);

ТестовыеКонфигурации = НайтиФайлы("cf", "*.cf");

Для каждого ТестоваяКонфигурация Из ТестовыеКонфигурации Цикл
	Лог.Информация("Обрабатывается конфигурация %1", ТестоваяКонфигурация.ПолноеИмя);
	ПротестироватьКонфигурацию(ТестоваяКонфигурация.ПолноеИмя);
КонецЦикла;
